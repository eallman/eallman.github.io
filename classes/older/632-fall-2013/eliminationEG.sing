// Elimination / Implicitization examples

// for help
// system("--browser", "mac");

// for a reduce Groebner basis, uncomment the following line
// option("redSB");

// Load elimination library
LIB "elim.lib";

// Actually the term order here does not matter, in the sense
// that when we "eliminate" t below, an elimination order is created.
ring r = 0,(y,z,t),lp;

// If you like to see that >lex is an elimination order, try this 
// alternative definition of the ring r, noting that t > y,z
// ring r = 0,(t,y,z),lp;

poly f1 = y-t^2;
poly f2 = z-t^3;

ideal i = f1, f2;
ideal stdI = std(i);
stdI;

// View help for different algorithms for elimination
//help elim;
//help elim1;
//help eliminate;

// Find the implicit description of the variety with parameterization
// given above.
ideal J = elim(i,t);
" ";
"Implicit description is given by ....";
J;


// A second example with two parameters.
ring s=0,(u,v,x,y,z),lp;

poly f1 = x-u2;
poly f2 = y-uv;
poly f3 = z-v^2;

ideal i = f1,f2,f3;
"The ideal i is generated by";
i;

ideal stdI = std(i);
"with Groebner basis given by ...";
stdI;

" ";
"Now we eliminate to get the implicit description .....";
ideal J = elim(i,uv);
J;

// Questions:  
//    What does the image of the real paramterization
//    look like?  What does V(J) look like?  Why geometrically
//    should this not be isomorphic to A^2?
